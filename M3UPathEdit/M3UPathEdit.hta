<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8" http-equiv="X-UA-Compatible" content="IE=10">
<script language="vbscript">
Const ForReading = 1
Const ForWriting = 2
Set oFSO = CreateObject("Scripting.FileSystemObject")
BaseFolder = "FDMusic"
Dim FirstLine

Sub window_onload
  document.title = "M3U Path Editor"
	x = 600
	y = 400
	Window.ResizeTo x, y
	Window.MoveTo (screen.availWidth - x)/2, (screen.availHeight - y)/2
  bf.value = BaseFolder
  cp.disabled = True
  np.disabled = True
  ub.disabled = True
End Sub

Sub SelectFile
  M3UFile = f1.value
  Set oFile = oFSO.OpenTextFile(M3UFile,ForReading)
  FirstLine = Trim(oFile.ReadLine)
  oFile.Close
  UpdatePath
End Sub

Sub ProcessFile
  If cp.value = np.value Then
    MsgBox "Nothing to update"
  Else
    M3UFile = f1.value
    Set oFile = oFSO.OpenTextFile(M3UFile,ForReading)
    Data = oFile.ReadAll
    oFile.Close
    Data = Replace(Data,cp.value,np.value)
    Set oFile = oFSO.OpenTextFile(M3UFile,ForWriting)
    oFile.Write Data
    oFile.Close
    MsgBox "Update complete"
    cp.value = np.value
  End If
End Sub

Sub UpdatePath
  np.disabled = True
  ub.disabled = True
  BaseFolder = Trim(bf.value)
  If BaseFolder="" Then
    MsgBox "Please set a base folder"
    Exit Sub
  End If
  BaseFolder = Replace(BaseFolder,"\","")
  BaseFolder = Replace(BaseFolder,"/","")
  BaseFolder = Replace(BaseFolder,":","")
  bf.value = BaseFolder
  If FirstLine<>"" Then
    PathPos = InstrRev(LCase(FirstLine),"\" & LCase(BaseFolder) & "\")
    If PathPos=0 Then
      MsgBox "Base folder '" & BaseFolder & "' not found in first line of M3U file"
    Else
      M3UPath = Left(FirstLine,PathPos+Len(BaseFolder)+1)
      cp.value = M3UPath
      np.value = M3UPath
      np.disabled = False
      ub.disabled = False
    End If
  End If
End Sub
</script>

<style>
body  {background-color:LightBlue; font-family:Segoe UI; font-size:11pt}
input[type="button"] {font-family:Segoe UI; font-size:11pt}
</style>
</head>
<body>
Base folder for M3U files: <input type=text id=bf  style=width:20em onchange=UpdatePath()><br><br>
Click the Browse button to select an M3U file to update:<br>
<input type=file id=f1 accept=".m3u" style=width:40em oninput=SelectFile()><br><br>
Current path in selected M3U file: <input type=text id=cp style=width:40em><br><br>
Edit the new path in the box below and then click the Update button:<br>
<input type=text id=np style=width:40em><br><br>
<input type=button id=ub value=Update onclick=ProcessFile()><br>
</body>
</html>
