<!DOCTYPE html>
<html>
<title >Time remaining</title>
<head>
<meta charset="UTF-8" http-equiv="X-UA-Compatible" content="IE=9">
<hta:application
  id=oHTA
  icon=perfmon.exe
  applicationname=TimeRemaining
  scroll=no
  contextmenu=no
  showintaskbar=no
  minimizeButton=no
  maximizeButton=no
  singleinstance=yes
  SysMenu=no
>
<script language="VBScript">
Const DefaultWait = 30 'minutes
Const HKCU = &H80000001

Set oWSH = CreateObject("Wscript.Shell")
Set oFSO = CreateObject("Scripting.FileSystemObject")
Set oReg = GetObject("winmgmts:\\.\root\default:StdRegProv")

Dim TimeLeftInSeconds,WaitTimer,Wait
MyFolder = oFSO.GetParentFolderName(Mid(document.URL,8))
oWSH.CurrentDirectory = MyFolder
AddTimeHTA = MyFolder & "\AddTime.hta"
logoutcmd = "Shutdown.exe -l -f"
Scale = GetScale()
w = 300 * Scale: h = 150 * Scale
Window.ResizeTo w, h
Window.MoveTo (screen.availWidth - w)/2, (screen.availHeight - h)/2

Sub window_onLoad
  Wait = DefaultWait
  TimeLeftInSeconds = Wait * 60
  UpdateCountdown
  WaitTimer = Window.SetInterval("UpdateCountdown()", 1000)
End Sub

Function document_onKeyDown()
  If window.event.keyCode=115 Or window.event.keyCode=116 Then
    window.event.keyCode = 0
    document_onKeyDown = False
  End If
End Function

Sub UpdateCountdown
  Hours = CInt(TimeLeftInSeconds \ 3600)
  Minutes = CInt((TimeLeftInSeconds Mod 3600) \ 60)
  Seconds = TimeLeftInSeconds Mod 60
  CountDown.innerHTML = Hours & ":" & Right("0" & Minutes,2) & ":" & Right("0" & Seconds,2)
  If TimeLeftInSeconds<=0 Then
    oWSH.Run logoutcmd,1,False
    self.Close
    Exit Sub
  End If
  TimeLeftInSeconds = TimeLeftInSeconds - 1
End Sub

Function GetScale()
  GetScale = 1.0
  Value = 0
  On Error Resume Next
  Value = oWSH.RegRead("HKCU\Control Panel\Desktop\Win8DpiScaling")
  If Value=1 Then
    'Custom scaling is set
    Value = 96
    Value = oWSH.RegRead("HKCU\Control Panel\Desktop\WindowMetrics\AppliedDPI")
    GetScale = Value/96
  Else
    'See if standard scaling is set
    Key = "Control Panel\Desktop\PerMonitorSettings"
    Result = oReg.EnumKey(HKCU, Key, ArrKeys)
    If Result=0 Then
      'Assume first monitor in list is the one we want
      For Each SubKey In ArrKeys
        Exit For
      Next
      Value = oWSH.RegRead("HKCU\" & Key & "\" & SubKey & "\DPIValue")
      If Value>0 Then GetScale = 1 + (Value * 0.25)
    End If
  End If
  On Error Goto 0
End Function

Sub AddTime
  ClearInterval(WaitTimer)
  oWSH.Run "elevate.exe -wait4exit mshta.exe " & AddTimeHTA & " " & window.screenLeft & " " & window.screenTop & " " & Scale,0,True
  On Error Resume Next
  MinutesToAdd = oWSH.RegRead("HKLM\Software\TimeRemainingHTA\AddTime")
  TimeStamp = oWSH.RegRead("HKLM\Software\TimeRemainingHTA\TimeStamp")
  TimeSinceLastEntry = DateDiff("s",TimeStamp,Now())
  On Error Goto 0
  If IsNumeric(MinutesToAdd) And TimeSinceLastEntry<2 Then 
    TimeLeftInSeconds = TimeLeftInSeconds + Int(MinutesToAdd * 60)
    UpdateCountdown
  End If
  WaitTimer = Window.SetInterval("UpdateCountdown()", 1000)
End Sub

</script>
<style>
body {background-color:black; font-family:Segoe UI; font-size:11pt}
h1 {color:red}
.timerDiv {text-align:center}
.timerButtons {width:6em}
</style>
</head>
<body>
  <div class=timerDiv>
    <h1 id=CountDown></h1>
    <input class=timerButtons type=button value='Add Time' OnClick=AddTime()>
  </div>
</body>
</html>