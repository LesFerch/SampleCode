<!DOCTYPE html>
<html>
<title >Time remaining</title>
<head>
<meta charset="UTF-8" http-equiv="X-UA-Compatible" content="IE=9">
<hta:application
  id=oHTA
  icon=perfmon.exe
  applicationname=TimeRemaining
  scroll=no
  contextmenu=no
  showintaskbar=yes
  minimizeButton=yes
  maximizeButton=no
  singleinstance=yes
  SysMenu=no
>
<script language="VBScript">
Const DefaultWait = 30 'minutes
Const WarningTime = 120 'seconds

Set oWSH = CreateObject("Wscript.Shell")
Set oFSO = CreateObject("Scripting.FileSystemObject")
Set oReg = GetObject("winmgmts:\\.\root\default:StdRegProv")

Dim TimeLeftInSeconds,WaitTimer,Wait
MyFolder = oFSO.GetParentFolderName(Mid(document.URL,8))
oWSH.CurrentDirectory = MyFolder
AddTimeHTA = MyFolder & "\AddTime.hta"
logoutcmd = "Shutdown.exe -l -f"
Scale = GetScale()
w = 300 * Scale: h = 150 * Scale
Window.ResizeTo w, h
Window.MoveTo (screen.availWidth - w)/2, (screen.availHeight - h)/2
Wait = DefaultWait
AddingTime = False

If InStr(MyFolder," ")>0 Then
  MsgBox "This script must be run from a folder with no spaces in its name",vbExclamation,"Error"
  self.close
End If

Architecture = oWSH.environment("PROCESS").item("PROCESSOR_ARCHITECTURE")
SystemRoot = oWSH.ExpandEnvironmentStrings("%SystemRoot%")
WOWPath = SystemRoot & "\SysWOW64\"
ExePath = SystemRoot & "\System32\"
ElevateExe = "elevate.exe"
If Architecture="AMD64" Then ElevateExe = "elevate64.exe"
If Architecture="x86" And oFSO.FolderExists(WOWPath) Then ExePath = WOWPath

On Error Resume Next
RegTimeLeft = oWSH.RegRead("HKCU\Software\TimeRemainingHTA\TimeLeftInSeconds")
TimeStamp = oWSH.RegRead("HKLM\Software\TimeRemainingHTA\TimeStamp")
TimeSinceLastEntry = DateDiff("s",TimeStamp,Now())
On Error Goto 0
If IsNumeric(RegTimeLeft) And TimeSinceLastEntry<2 Then
  TimeLeftInSeconds = RegTimeLeft
Else
  TimeLeftInSeconds = Wait * 60
End If

Sub window_onLoad
  UpdateCountdown
  WaitTimer = Window.SetInterval("UpdateCountdown()", 1000)
End Sub

Function document_onKeyDown()
  If window.event.keyCode=115 Or window.event.keyCode=116 Then
    window.event.keyCode = 0
    document_onKeyDown = False
  End If
End Function

Sub UpdateCountdown
  Hours = CInt(TimeLeftInSeconds \ 3600)
  Minutes = CInt((TimeLeftInSeconds Mod 3600) \ 60)
  Seconds = TimeLeftInSeconds Mod 60
  CountDown.innerHTML = Hours & ":" & Right("0" & Minutes,2) & ":" & Right("0" & Seconds,2)
  If TimeLeftInSeconds<=0 Then
    oWSH.Run logoutcmd,1,False
    self.Close
    Exit Sub
  End If
  If Not AddingTime Then TimeLeftInSeconds = TimeLeftInSeconds - 1
  oWSH.RegWrite "HKCU\Software\TimeRemainingHTA\TimeLeftInSeconds",TimeLeftInSeconds
  If TimeLeftInSeconds=WarningTime Then oWSH.Run "Powershell.exe -NoLogo -ExecutionPolicy Bypass -File .\Warning.ps1",0,False
End Sub

Function GetScale()
  GetScale = 1.0
  On Error Resume Next
  'If user changes scale, they must logout/login for this registry value to change
  GetScale = oWSH.RegRead("HKCU\Control Panel\Desktop\WindowMetrics\AppliedDPI") / 96
  On Error Goto 0
End Function

Sub AddTime
  AddingTime = True
  oWSH.Run ElevateExe & " -wait4exit " & ExePath & "mshta.exe """ & AddTimeHTA & """ " & window.screenLeft & " " & window.screenTop & " " & Scale,0,True
  On Error Resume Next
  MinutesToAdd = oWSH.RegRead("HKLM\Software\TimeRemainingHTA\AddTime")
  TimeStamp = oWSH.RegRead("HKLM\Software\TimeRemainingHTA\TimeStamp")
  TimeSinceLastEntry = DateDiff("s",TimeStamp,Now())
  On Error Goto 0
  If IsNumeric(MinutesToAdd) And TimeSinceLastEntry<2 Then 
    TimeLeftInSeconds = TimeLeftInSeconds + Int(MinutesToAdd * 60)
    UpdateCountdown
  End If
  AddingTime = False
End Sub

</script>
<style>
body {background-color:black; font-family:Segoe UI; font-size:11pt}
h1 {color:red}
.timerDiv {text-align:center}
.timerButtons {width:6em}
</style>
</head>
<body>
  <div class=timerDiv>
    <h1 id=CountDown></h1>
    <input class=timerButtons type=button value='Add Time' OnClick=AddTime()>
  </div>
</body>
</html>