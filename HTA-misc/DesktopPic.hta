<!DOCTYPE html>
<html>
<head>
<title>DesktopPic</title>
<meta charset="UTF-8" http-equiv="X-UA-Compatible" content="IE=9">
<hta:application
  id=oHTA
  icon=colorcpl.exe
  showintaskbar=no
  border=none
  caption=no
  scroll=no
>
<script language="VBScript">

'Begin Main

Window.ResizeTo 1,1
Window.MoveTo -2000,-2000

Const ForReading = 1
Const ForWriting = 2
Set oWSH = CreateObject("Wscript.Shell")
Set oFSO = CreateObject("Scripting.FileSystemObject")
Set oImg = CreateObject("WIA.ImageFile")
Set oPicList = CreateObject("System.Collections.ArrayList")
Set oLang = CreateObject("System.Collections.ArrayList")
Set oSettings = CreateObject("Scripting.Dictionary")
Set DigitsOnly = New RegExp: DigitsOnly.Pattern = "[^0-9]"
Set DigitsComma = New RegExp: DigitsComma.Pattern = "[^0-9,]"
Dim PicTimer,UpdatePos,w,h,iw,ih,CenterCoordinates,hTpp,wTpp,M1,M2,ArrLang
Z = VBCRLF

oLang.add "Place at least one image file in |No image files found|Run on startup|Bring to front on start|Stretch smaller pictures|Pic delay in seconds|Max percent of screen area|Centered|Custom|Window position|Continue"
oLang.add "Platzieren Sie mindestens eine Bilddatei in |Keine Bilddateien gefunden|Beim Start ausführen|Beim Start nach vorne bringen|Strecken Sie kleinere Bilder|Bildverzögerung in Sekunden|Maximaler Prozentsatz der Bildschirmfläche|Zentriert|Brauch|Fensterposition|Fortsetzen"
oLang.add "Coloque al menos un archivo de imagen en |No se encontraron archivos de imagen|Corre al empezar|Traer al frente al inicio|Estirar imágenes más pequeñas|Retardo de imagen en segundos|Porcentaje máximo del área de la pantalla|Centrado|Personalizado|Posición de la ventana|Continuar"
oLang.add "Placez au moins un fichier image dans |Aucun fichier image trouvé|Exécuter au démarrage|Amenez à l'avant au démarrage|Étirez des images plus petites|Délai d'image en secondes|Pourcentage maximum de la zone d'écran|Centré|Personnalisé|Position de la fenêtre|Continuez"
oLang.add "少なくとも1つの画像ファイルを配置してに |画像ファイルが見つかりません|起動時に実行する|スタート時に前面に出す|小さい画像を伸ばす|秒単位の画像の遅延|画面領域の最大パーセント|中央揃え|カスタム|ウィンドウの位置|継続する"
oLang.add "将至少一个图像文件放入|未找到图像文件|在启动时运行|开始时放在前面|拉伸较小的图片|图片延迟以秒为单位|最大屏幕面积百分比|居中|风俗|窗口位置|继续"
oLang.add "將至少一個圖像文件放入 |未找到圖像文件|在啟動時運行|未未找到圖像文件找到图像文件開始時放在前面|拉伸較小的圖片|圖片延遲以秒為單位|最大屏幕面積百分比|居中|風俗|窗口位置|繼續"

RunOnStartup = False
BringToFront = True
Stretch = False
PicDelay = 5
MaxScrPct = 25
Coordinates = ""
Forward = True
i = -1
LangIdx=0
On Error Resume Next
Language = oWSH.RegRead("HKCU\Control Panel\International\LocaleName")
Language = oWSH.RegRead("HKCU\Control Panel\Desktop\PreferredUILanguages")(0)
If Left(Language,2)="de" Then LangIdx=1
If Left(Language,2)="es" Then LangIdx=2
If Left(Language,2)="fr" Then LangIdx=3
If Left(Language,2)="ja" Then LangIdx=4
If Language="zh-CN" Then LangIdx=5
If Language="zh-TW" Then LangIdx=6
On Error Goto 0

OptionMode = oHTA.caption="yes"
BorderSize = 0: If OptionMode Then BorderSize = 8

CmdLine = Replace(oHTA.commandLine,Chr(34) & "  " & Chr(34),"|")
CmdLine = Replace(CmdLine,Chr(34) & " " & Chr(34),"|")
CmdLine = Replace(CmdLine,Chr(34),"")
aParam = Split(CmdLine,"|")

MyPath  = Mid(document.url,8)
MyFolder = oFSO.GetParentFolderName(MyPath)
MyName = oFSO.GetFileName(MyPath)
MyTitle = Split(MyName,".")(0)

If UBound(aParam)>0 Then
  If IsNumeric(aParam(1)) Then i = aParam(1) - 1
End If
If OptionMode Then
  PicPath = aParam(2)
  HTAName = aParam(3)
  MyTitle = Split(HTAName,".")(0)
  oWSH.CurrentDirectory = PicPath
End If

If OptionMode Then
  oFSO.DeleteFile(MyPath)
Else
  oWSH.CurrentDirectory = MyFolder
End If

PicPath = oWSH.CurrentDirectory & "\"
INIFile = PicPath & MyTitle & ".ini"

Ini2Dict

If oSettings.Exists("[Options]LangIdx") Then LangIdx = oSettings.Item("[Options]LangIdx")
If oSettings.Exists("[Options]RunOnStartup") Then RunOnStartup = (LCase(oSettings.Item("[Options]RunOnStartup"))="true")
If oSettings.Exists("[Options]BringToFront") Then BringToFront = (LCase(oSettings.Item("[Options]BringToFront"))="true")
If oSettings.Exists("[Options]Stretch") Then Stretch = (LCase(oSettings.Item("[Options]Stretch"))="true")
If oSettings.Exists("[Options]PicDelay") Then PicDelay = oSettings.Item("[Options]PicDelay")
If oSettings.Exists("[Options]MaxScrPct") Then MaxScrPct = oSettings.Item("[Options]MaxScrPct")
If oSettings.Exists("[Options]Coordinates") Then Coordinates = oSettings.Item("[Options]Coordinates")
If Stretch="" Then Stretch = False
If PicDelay="" Then PicDelay = 5
If MaxScrPct="" Then MaxScrPct = 25

UpdateLangPrep

sw = screen.availWidth
sh = screen.availHeight

Set oFolder = oFSO.GetFolder(PicPath)
Set oFiles = oFolder.Files
For Each oFile In oFiles
  On Error Resume Next
  oImg.LoadFile(oFile.Name)
  If Err.Number=0 Then IsImage=True Else IsImage=False
  On Error Goto 0
  If IsImage Then
    iw = oImg.Width: ih = oImg.Height
    oPicList.Add oFile.Name & "|" & iw & "|" & ih
  End If
Next

If oPicList.Count=0 Then
  MsgBox M1 & MyFolder,vbOKonly,M2
  self.close
End If

'End Main


Sub Ini2Dict
  If oFSO.FileExists(INIFile) Then 
    Set oFile = oFSO.OpenTextFile(INIFile,ForReading)
    Do Until oFile.AtEndOfStream
      Line = Trim(oFile.ReadLine)
      If Line<>"" And Left(Line,1)<>";" Then
        If Left(Line,1)="[" Then
          Section = Line
        Else
          ArrLine = Split(Line,"=")
          If UBound(Arrline)=1 Then oSettings.Add Section & ArrLine(0),ArrLine(1)
        End If
      End If
    Loop
    oFile.Close
  End If
End Sub

Sub window_onLoad

  If OptionMode Then
    ShowOptions
    If LangIdx<>0 Then UpdateLang
  Else
    HideOptions
  End If
  
  Document.Title = MyTitle
  If Not BringToFront Then Window.blur

  LoadPic

  If Coordinates<>"" Then RepositionWindow

  ToggleShortcut

  wTpp = Window.outerWidth: hTpp = Window.outerHeight

  If OptionMode Then
    UpdatePos = Window.SetInterval("UpdatePosition()", 100)
  Else
    If oPicList.Count>1 Then PicTimer = Window.SetInterval("LoadPic()", PicDelay*1000)
  End If

End Sub

Sub UpdateLangPrep
  ArrLang = Split(oLang(LangIdx),"|")
  M1 = ArrLang(0)
  M2 = ArrLang(1)
End Sub

Sub UpdateLang
  UpdateLangPrep
  L1.innerHTML = ArrLang(2)
  L2.innerHTML = ArrLang(3)
  L3.innerHTML = ArrLang(4)
  L4.innerHTML = ArrLang(5)
  L5.innerHTML = ArrLang(6)
  L6.innerHTML = ArrLang(7)
  L7.innerHTML = ArrLang(8)
  L8.innerHTML = ArrLang(9)
  Bcn.Value = ArrLang(10)
End Sub

Sub RepositionWindow
  aCoords = Split(Coordinates,",")
  x = aCoords(0)
  y = aCoords(1)
  Window.MoveTo x - BorderSize, y
End Sub

Sub ToggleShortcut
  RunOnstartup = Crs.Checked
  ShortCutFolder = oWSH.SpecialFolders("Startup")
  ShortcutExists = False
  Set oFolder = oFSO.GetFolder(ShortCutFolder)
  Set oFiles = oFolder.Files
  For Each oFile In oFiles
    If LCase(oFSO.GetExtensionName(oFile.Name)) = "lnk" Then
      Set link = oWSH.CreateShortcut(ShortCutFolder & "\" & oFile.Name)
      If link.TargetPath=PicPath & HTAName Then
        ShortcutExists = True
        If Not RunOnstartup Then oFSO.DeleteFile ShortCutFolder & "\" & oFile.Name
      End If
    End If
  Next
  If RunOnstartup And Not ShortcutExists Then
    ShortCutPath = ShortCutFolder & "\" & MyTitle & Replace(Replace(Replace(Now(),"/",""),":","")," ","") & ".lnk"
    Set link = oWSH.CreateShortcut(ShortCutPath)
    link.TargetPath = PicPath & HTAName
    link.Save
  End If
End Sub

Sub UpdatePosition
  w = Window.outerWidth
  h = Window.outerHeight
  UpdateTpp = Not(w=wTpp And h=hTpp)
  w = w - (BorderSize * 2)
  h = h - BorderSize
  If UpdateTpp Then
    NewSize = Cint((w * h) / (sw * sh) * 100)
    If NewSize>100 Then NewSize = 100
    Tpp.Value = NewSize
  Else
    Tpp.Value = MaxScrPct
  End If
  Tsp.Value = window.screenX + BorderSize & "," & window.screenY
  If Mpp.SelectedIndex=0 And Tsp.Value<>CenterCoordinates Then
    Mpp.SelectedIndex=1
    Tsp.style.visibility = "visible"
  End If
End Sub

Sub LoadPic
  If Forward Then i = i + 1 Else i = i - 1
  If i=oPicList.Count Then i = 0
  If i<0 Then i = oPicList.Count - 1
  aImage = Split(oPicList(i),"|")
  iw = aImage(1): ih = aImage(2)
  ResizePic
  window.document.body.background = PicPath & aImage(0)
  ResizeWindow
  If Coordinates="" Then CenterWindow
End Sub

Sub ResizePic
  MaxArea = sw * sh * (MaxScrPct/100)
  ImgArea = iw * ih
  w = iw: h = ih
  If ImgArea>MaxArea Or Stretch Or OptionMode Then
    ResizeFactor = Sqr(MaxArea/ImgArea)
    w = iw * ResizeFactor
    h = ih * ResizeFactor
    If w>sw Then h = sw/w * h : w = sw
    If h>sh Then w = sh/h * w : h = sh
    w = CInt(w): h = CInt(h)
    h = h + BorderSize: w = w + (BorderSize * 2)
  End If
End Sub

Sub ResizeWindow
	Window.ResizeTo w, h
End Sub

Sub CenterWindow
	Window.MoveTo (sw - w)/2, (sh - h)/2
  CenterCoordinates = window.screenX + BorderSize & "," & window.screenY
End Sub

Sub ShowOptions
  document.body.style.visibility = "visible"
  Crs.Style.Zoom = 1.2
  Cbf.Style.Zoom = 1.2
  Csp.Style.Zoom = 1.2
  Crs.Checked = RunOnStartup
  Cbf.Checked = BringToFront
  Csp.Checked = Stretch
  Tpd.Value = PicDelay
  Tpp.Value = MaxScrPct
  Tsp.Value = Coordinates
  If Coordinates="" Then
    Tsp.style.visibility = "hidden"
  Else
    mpp.SelectedIndex = 1
    Tsp.style.visibility = "visible"
  End If
  Mln.SelectedIndex = LangIdx
End Sub

Sub HideOptions
  document.body.style.visibility = "hidden"
  Tsp.style.visibility = "hidden"
End Sub

Sub document_onKeyDown
  Select Case window.event.keyCode
    Case 32, 39
      If Not OptionMode Then 
        HideOptions
        Forward = True
        clearInterval(PicTimer)
        LoadPic
      End If
    Case 8, 37
      If Not OptionMode Then 
        HideOptions
        Forward = False
        clearInterval(PicTimer)
        LoadPic
      End If
    Case 13
      ContinueShow
    Case 27
      If Not OptionMode Then LaunchTempHTA
  End Select
End Sub

Sub ContinueShow
  If OptionMode Then
    oWSH.Run Chr(34) & PicPath & HTAName & Chr(34) & " " & Chr(34) & i & Chr(34),1,False
    self.close
  Else
    HideOptions
    Forward = True
    clearInterval(PicTimer)
    PicTimer = Window.SetInterval("LoadPic()", PicDelay*1000)
  End If
End Sub

Sub LaunchTempHTA
  clearInterval(PicTimer)
  oWSH.CurrentDirectory = MyFolder
  Temp  = oWSH.ExpandEnvironmentStrings("%Temp%")
  TempHTA = Temp & "\" & Replace(Replace(Replace(Now(),"/",""),":","")," ","") & ".hta"
  Set oFile = oFSO.OpenTextFile(MyPath,ForReading)
  MyCode = oFile.ReadAll
  oFile.Close
  MyCode = Replace(MyCode,LCase("CAPTION=NO"),"caption=yes")
  MyCode = Replace(MyCode,LCase("BORDER=NONE"),"border=thick")
  MyCode = Replace(MyCode,LCase("SCROLL=NO"),"scroll=auto")
  Set oFile = oFSO.OpenTextFile(TempHTA,ForWriting,True)
  oFile.Write MyCode
  oFile.Close
  oWSH.Run TempHTA & " " & Chr(34) & i & Chr(34) & " " & Chr(34) & MyFolder & Chr(34) & " " & Chr(34) & MyName & Chr(34),1,False
  self.close
End Sub

Sub OnFocusInput
  ClearInterval(UpdatePos)
End Sub

Sub OnBlurInput
  UpdatePos = Window.SetInterval("UpdatePosition()", 500)
End Sub

Sub OnInputDigits(id)
  document.getElementByID(id).Value = DigitsOnly.Replace(document.getElementByID(id).Value,"")
End Sub

Sub OnInputDigitsComma(id)
  document.getElementByID(id).Value = DigitsComma.Replace(document.getElementByID(id).Value,"")
End Sub

Sub OnChangeTpd
  If Tpd.Value<1 Then Tpd.Value = 1
End Sub

Sub OnChangeTpp
  If Tpp.Value<1 Or Tpp.Value>100 Then
    Tpp.Value = MaxScrPct
  Else
    MaxScrPct = Tpp.Value
  End If
  ResizePic
  ResizeWindow
  wTpp = Window.outerWidth: hTpp = Window.outerHeight
End Sub

Sub OnChangeMPP
  If Mpp.SelectedIndex=1 Then
    Tsp.style.visibility = "visible"
  Else
    CenterWindow
    Tsp.style.visibility = "hidden"
  End If
End Sub

Sub OnChangeMln
  LangIdx = Mln.SelectedIndex
  UpdateLang
End Sub

Sub OnChangeTsp
  If tsp.Value<>"" Then 
    Coordinates = Tsp.Value
    If Left(Coordinates,1)="," Then Coordinates = "0" & Coordinates
    If Not Instr(Coordinates,",") Then Coordinates = Coordinates & ",0"
    RepositionWindow
  End If
End Sub

Sub window_onUnload
  If OptionMode Then Var2Ini
End Sub

Sub Var2Ini
  Set oFile = oFSO.OpenTextFile(INIFile,ForWriting,True)
  oFile.Write "[Options]" & Z
  oFile.Write "LangIdx=" & LangIdx & Z
  oFile.Write "RunOnStartup=" & Crs.Checked & Z
  oFile.Write "BringToFront=" & Cbf.Checked & Z
  oFile.Write "Stretch=" & Csp.Checked & Z
  oFile.Write "PicDelay=" & Tpd.Value & Z
  oFile.Write "MaxScrPct=" & Tpp.Value & Z
  If Mpp.SelectedIndex=0 Then Tsp.Value=""
  oFile.Write "Coordinates=" & Tsp.Value & Z
  oFile.Close
End Sub

</script>
<style>
body {background-size:cover; background-attachment:fixed; background-repeat:no-repeat}
br {font-size: 16pt} 
.cc {
font-family: sans-serif;
font-weight: normal;
font-size: 11pt;
color: white;
background: rgba(0,0,0,.4);
padding: 1pt;
border-radius: 2pt;
}
</style>
</head>
<body>
<select id=Mln  OnChange=OnChangeMln()>
<option>English</option>
<option>Deutsch</option>
<option>Español</option>
<option>Français</option>
<option>日本語</option>
<option>中文 (简体)</option>
<option>中文 (繁體)</option>
</select><br>
<input type=checkbox id=Crs OnClick=ToggleShortcut()>
<span id=L1 class=cc>Run on startup</span><br>
<input type=checkbox id=Cbf>
<span id=L2 class=cc>Bring to front on start</span><br>
<input type=checkbox id=Csp>
<span id=L3 class=cc>Stretch smaller pictures</span><br>
<input type=text size=5 id=Tpd MaxLength=5 OnFocus=OnFocusInput() OnBlur=OnBlurInput() OnInput=OnInputDigits("Tpd") OnChange=OnChangeTpd()>
<span id=L4 class=cc>Pic delay in seconds</span><br>
<input type=text size=5 id=Tpp MaxLength=3 OnFocus=OnFocusInput() OnBlur=OnBlurInput() OnInput=OnInputDigits("Tpp") OnChange=OnChangeTpp()>
<span id=L5 class=cc>Max percent of screen area</span><br>

<select id=Mpp  OnChange=OnChangeMpp()>
<option id=L6>Centered</option>
<option id=L7>Custom</option>
</select>
<span id=L8 class=cc>Window position</span>
<input type=text size=11 id=Tsp MaxLength=11 OnFocus=OnFocusInput() OnBlur=OnBlurInput() OnInput=OnInputDigitsComma("Tsp") OnChange=OnChangeTsp()>
<br><br>
<input type=button id=Bcn Value=Continue onclick=ContinueShow()>

</body>
</html>
